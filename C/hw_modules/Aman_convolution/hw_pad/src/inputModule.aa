// Access the input tensor and write to iput pipe
$module [access_T]
$in(row_in ct chl_in chl_out rk pad : $uint<16>)
$out()
$is
{
	$branchblock [access_block]
	{	
		ct_new := (ct + (pad<<1))
		row_size_total := ($bitcast ($uint<32>) (ct_new * chl_in))
		p_chl_in := ($bitcast ($uint<32>) (pad*chl_in))
		m_factor := ($bitcast ($uint<32>) (ct*chl_in))
		m2_factor := (m_factor << 1)
		m3_factor := (m2_factor+m_factor)
		m4_factor := (m2_factor << 2)
		mode_3 := (rk == 3)

		// Pipeline for sending inputs
		$dopipeline $depth 15 $buffering 2 $fullrate
		$merge $entry $loopback
		$phi address := (-1*(($bitcast ($uint<32>) pad)*m_factor)) $on $entry n_address $on $loopback
		$phi row1 := ($bitcast ($uint<16>) 0) $on $entry n_row1 $on $loopback
		$phi chl_num := ($bitcast ($uint<16>) 0) $on $entry n_chl_num $on $loopback
		$phi mycounter := ($bitcast ($uint<32>) 1) $on $entry n_mycounter $on $loopback
		$endmerge
		
		$call timer (4) (time_pipe)
		$volatile next_row :=  (mycounter == row_size_total)
		$volatile tmp_cnt := (mycounter + 1)
		$volatile n_mycounter := ($mux next_row 1 tmp_cnt )
		$volatile update_row := ($mux next_row (row1 + N_ROWS_S) row1 )
		$volatile continue_flag1 := (update_row < row_in)
		$volatile mc_lt_p := ((mycounter <= p_chl_in) | (mycounter > (row_size_total - p_chl_in))) 
		
		$volatile n_row1 := ($mux continue_flag1 update_row 0)
		$volatile n_address := ($mux continue_flag1 (address + ($mux next_row (m_factor + 1) ($mux mc_lt_p 0 1))) 0)
		
	 	$volatile p1_zero := (mc_lt_p | ((row1 < pad) | (row1 >= (row_in - (pad-3)))))
	 	$volatile p2_zero := (mc_lt_p | ((row1 < (pad-1)) | (row1 >= (row_in - (pad-2)))))
	 	$volatile p3_zero := (mc_lt_p | ((row1 < (pad-2)) | (row1 >= (row_in - (pad-1)))))
	 	$volatile p4_zero := (mc_lt_p | ((row1 < (pad-3)) | (row1 >= (row_in - pad))))
		$volatile addr2 := (address+ m_factor)
		$volatile addr3 := (address+ m2_factor)
		$volatile addr4 := (address+ m3_factor)
		$guard (~p1_zero) val1 := T[0][(address>>3)]
		$guard (~p2_zero) val2 := T[0][(addr2>>3)]
		$guard (~p3_zero) val3 := T[0][(addr3>>3)]
		$guard (~p4_zero) val4 := T[0][(addr4>>3)]

		$volatile rd1 := ($mux p1_zero 0 (val1 >> ($bitcast ($uint<64>) ((7-(address & 7))<<3))))
		$volatile rd2 := ($mux p2_zero 0 (val2 >> ($bitcast ($uint<64>) ((7-(addr2 & 7))<<3))))
		$volatile rd3 := ($mux p3_zero 0 (val3 >> ($bitcast ($uint<64>) ((7-(addr3 & 7))<<3))))
		$volatile rd4 := ($mux p4_zero 0 (val4 >> ($bitcast ($uint<64>) ((7-(addr4 & 7))<<3))))
		core_ip1 := ($bitcast ($uint<8>) rd1)
		core_ip2 := ($bitcast ($uint<8>) rd2)
		core_ip3 := ($bitcast ($uint<8>) rd3)
		$guard (mode_3) core_ip4 := ($bitcast ($uint<8>) rd4)

		$volatile n_chl_num := ($mux continue_flag1 chl_num (chl_num + 1))
		$volatile not_end_flag := ((n_chl_num != chl_out) | continue_flag1)

		$while not_end_flag

		// $call timer (101) (time_pipe)
	}
}
